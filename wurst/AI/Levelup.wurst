package Levelup

import LinkedList
import ClosureEvents

constant DEBUG = false

public class Levelup
  int heroId
  LinkedList<int> techTree
  EventListener trig

  construct(int id)
    techTree = new LinkedList<int>()
    heroId = id

  function addTree(vararg int trees)
    for i in trees
      techTree.push(i)

  function syncAbils(unit u)
    if DEBUG
      printTimed(u.getName() + " level up", 5.)
    let curPoint = u.getSkillPoints()
    for int i = 1 to curPoint
      let skill = techTree.dequeue()
      if DEBUG
        printTimed(GetObjectName(skill) + " taken to level " + (u.getAbilityLevel(skill) + 1).toString(), 5.)
      u.selectSkill(skill)

  function create()
    trig = EventListener.add(EVENT_PLAYER_HERO_LEVEL) ->
      let u = EventData.getLevelingUnit()
      let cond = u.getOwner().getController() == MAP_CONTROL_COMPUTER and u.getTypeId() == this.heroId
      if cond
        syncAbils(u)

